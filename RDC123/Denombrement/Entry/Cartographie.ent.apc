{Application 'CARTOGRAPHIE' logic file generated by CSPro}
PROC GLOBAL

{ Definitions of working variables }

set explicit
numeric t i j x ex=0 addmode ;

file FichSaPart Cartokml   ;
string strpart xkml eKML xrepGoo vcom vquart ;
array codes(50) ;
array alpha(30) labels(50);
alpha(900) strnotes;               { for editnote at the end of the interview }


// sauvegarde des données 
Function sav()
	savepartial();
end;

{function OnKey()
z=0;
  if z=118 then {F7}
    OnKey = z; { return rest of keys }
  endif;
end;
}


// Quitter
Function quitter()
	savepartial();
	stop(1) ;
end;

  { setup basic user bar }
  function userbase();
    userbar( clear );

    
    userbar(add text,"DENOMBREMENT ");
	userbar(set color,236,254,255);
	
    userbar( add button, "<",    do("PreviousField") );
    userbar( add button, ">",    do("NextField") );	
    userbar( add button, "Note", do("EditNote") );  
 	userbar(add spacing,25);   
	userbar(add button,"Sauvegarde", sav());    
	userbar(add spacing,25);
	userbar(add text," ");
	
	userbar(add button,"Quitter", quitter());    
	userbar(add spacing,25);
	userbar(add text," ");
  end;


function clear_labels();
	do i = 1 while i <= 50
	  codes(i) = notappl;
	  labels(i) = "";
	enddo;
end;


Function rech_com()
  t=0; i=1; 
      setfile( FichSaPart,"C:\RDC123\Denombrement\Reference\Commune.txt") ;  
	     if Filesize( FichSaPart ) > 0 then
		     while FileRead( FichSaPart, strpart ) do
		          if tonumber(strpart[1:2]) = NG01 then  
                  codes(i)=tonumber(strpart[3:3]);
                  labels(i)=strip(strpart[6:20]);
		               if NG02=tonumber(strpart[3:3]) then
		               vcom=strip(strpart[6:20]);
		               endif;
                  i=i+1;
		          endif ;
		      enddo;
          endif ;
         close( FichSaPart );
end;


Function rech_quartier()
  t=0; i=1; 
      setfile( FichSaPart,"C:\RDC123\Denombrement\Reference\Quartier.txt") ;  
	     if Filesize( FichSaPart ) > 0 then
		     while FileRead( FichSaPart, strpart ) do
		          if tonumber(strpart[1:2]) = NG01 and tonumber(strpart[3:3]) = NG02 then  
                  codes(i)=tonumber(strpart[6:3]);
                  labels(i)=strip(strpart[9:30]);
		                 if NG03=tonumber(strpart[6:3]) then
				              vquart=strip(strpart[9:30]);
				         endif;
                  i=i+1;
		          endif ;
		      enddo;
          endif ;
         close( FichSaPart );
end;


Function rech_Ilot()
  t=0; i=1; 
      setfile( FichSaPart,"C:\RDC123\Denombrement\Reference\Ilot.txt") ;  
	     if Filesize( FichSaPart ) > 0 then
		     while FileRead( FichSaPart, strpart ) do
		          if tonumber(strpart[1:2]) = NG01 and tonumber(strpart[3:3]) = NG02  and tonumber(strpart[6:3]) = NG03 then  
                  codes(i)=tonumber(strpart[9:5]);
                  labels(i)="Ilot"+"_"+strpart[9:5];
                  i=i+1;
		          endif ;
		      enddo;
          endif ;
         close( FichSaPart );
end;

Function rech_Avenue()
  t=0; i=1; 
      setfile( FichSaPart,"C:\RDC123\Denombrement\Reference\Avenue.txt") ;  
	     if Filesize( FichSaPart ) > 0 then
		     while FileRead( FichSaPart, strpart ) do
		          if tonumber(strpart[1:5]) = NG04 then  
                  codes(i)=i;
                  labels(i)=strip(strpart[6:30]);
                  i=i+1;
		          endif ;
		      enddo;
		      codes(i)=i;
                  labels(i)="A saisir";
                  i=i+1;
          endif ;
         close( FichSaPart );
end;


function parcelle()

				filewrite(Cartokml, '<Style id="my_style">"');
				filewrite(Cartokml, "  <IconStyle>");
				filewrite(Cartokml, "     <Icon>");
				filewrite(Cartokml,concat("       <href> ","C:\RDC123\Denombrement\Reference\icons","\parcelle.png"," </href>"));
				filewrite(Cartokml,"      </Icon>");
				filewrite(Cartokml,"   </IconStyle>");
				filewrite(Cartokml,"</Style>");				
				
										
				filewrite(Cartokml,"<Placemark>");
				filewrite(Cartokml,concat("    <name>","Parcelle : ", edit("999",NG06) , "</name>" ));
				filewrite(Cartokml, concat("<description><![CDATA[Province : ",strip(getlabel(NG01_VS1,NG01)),"<br />", " Commune : " ,strip(vcom),"<br />" ,"Quartier : ",strip(vquart),"<br />",
				" ILOT ",edit("99999",NG04),"<br />",
				" Rue / Avenue  ",strip(R02),"<br />", " N° parcelle ",R03,"]]></description>"));
											
				filewrite(Cartokml,"<styleUrl>#my_style</styleUrl>");
				filewrite(Cartokml,"<Point>");	
				filewrite(Cartokml, concat("    <coordinates>",edit("99.999999",R07),",",edit("99.999999",R06) ,",","0.000000</coordinates>"));
				filewrite(Cartokml,"</Point>");
				filewrite(Cartokml,"</Placemark>");

end;

function bati()
do varying i=1 until i>R04A 
			
										
				filewrite(Cartokml,"<Placemark>");
				filewrite(Cartokml,concat("    <name>","B : ", edit("999",NG06),"/" , edit("999",B01(i)) , "</name>" ));
				filewrite(Cartokml, concat("<description><![CDATA[Province : ",strip(getlabel(NG01_VS1,NG01)),"<br />", " Commune : " ,strip(vcom),"<br />" ,"Quartier : ",strip(vquart),"<br />",
				" ILOT ",edit("99999",NG04),"<br />",
				" Rue / Avenue  ",strip(R02),"<br />", " N° parcelle ",R03, " N° bâti ",edit("999",B01(i)),"]]></description>"));
											
				filewrite(Cartokml,"<styleUrl>#my_style</styleUrl>");
				filewrite(Cartokml,"<Point>");	
				filewrite(Cartokml, concat("    <coordinates>",edit("99.999999",B06(i)),",",edit("99.999999",B05(i)) ,",","0.000000</coordinates>"));
				filewrite(Cartokml,"</Point>");
				filewrite(Cartokml,"</Placemark>");
enddo;
end;


function menage()
do varying i=1 until i>R08
			
			filewrite(Cartokml, '<Style id="my_style">"');
				filewrite(Cartokml, "  <IconStyle>");
				filewrite(Cartokml, "     <Icon>");
				//filewrite(Cartokml,concat("       <href> ","C:\RDC123\Denombrement\Reference\icons","\Women_HH.png"," </href>"));
				if M05(i)=1 then
							filewrite(Cartokml,concat("       <href> ","C:\RDC123\Denombrement\Reference\icons","\homme_HH.png"," </href>"));
				else 
							filewrite(Cartokml,concat("       <href> ","C:\RDC123\Denombrement\Reference\icons","\Women_HH.png"," </href>"));
				endif;
							
				filewrite(Cartokml,"      </Icon>");
				filewrite(Cartokml,"   </IconStyle>");
				filewrite(Cartokml,"</Style>");	
	
										
				filewrite(Cartokml,"<Placemark>");
				filewrite(Cartokml,concat("    <name>","M : ", edit("999",NG06) ,"/",edit("999",M01(i)) ,"/",edit("999",M02(M01)) , "</name>" ));
				filewrite(Cartokml, concat("<description><![CDATA[Province : ",strip(getlabel(NG01_VS1,NG01)),"<br />", " Commune : " ,strip(vcom),"<br />" ,"Quartier : ",strip(vquart),"<br />",
				" ILOT ",edit("99999",NG04),"<br />",
				" Rue / Avenue  ",strip(R02),"<br />", " N° parcelle ",R03,"<br />", " N° bâti ",edit("99",M01(i)),"<br />", " N° ménage ",edit("99",M02(i)),"<br />", " Chef de ménage : ",strip(M04(i)),"<br />","]]></description>"));
											
				filewrite(Cartokml,"<styleUrl>#my_style</styleUrl>");
				filewrite(Cartokml,"<Point>");	
				filewrite(Cartokml, concat("    <coordinates>",edit("99.999999",M12(i)),",",edit("99.999999",M11(i)) ,",","0.000000</coordinates>"));
				filewrite(Cartokml,"</Point>");
				filewrite(Cartokml,"</Placemark>");
enddo;
end;

function rempli_coord()
	
		        Setfile(Cartokml, "C:\RDC123\Denombrement\Reference\Cartes\CartoIlot.kml");	
			    filecreate(Cartokml);
			    filewrite(Cartokml, concat('<?xml version="1.0"', ' encoding="UTF-8"?>'));
			    filewrite(Cartokml, '<kml xmlns="http://www.opengis.net/kml/2.2" >');
		
			    filewrite(Cartokml, '<Document>');
			    filewrite(Cartokml, "<Folder>");

				parcelle();
			
				filewrite(Cartokml, '<Style id="my_style">"');
				filewrite(Cartokml, "  <IconStyle>");
				filewrite(Cartokml, "     <Icon>");
				filewrite(Cartokml,concat("       <href> ","C:\RDC123\Denombrement\Reference\icons","\bati.png"," </href>"));
				filewrite(Cartokml,"      </Icon>");
				filewrite(Cartokml,"   </IconStyle>");
				filewrite(Cartokml,"</Style>");				
		
				bati();
				
							
			
				menage();
				filewrite(Cartokml, "</Folder>");
				filewrite(Cartokml, '</Document>');
				  
				filewrite(Cartokml, "</kml>");
				  
				close(Cartokml);
										
end ;


function Afficher_carte()

if Getos() < 20 then		
		//recherche l'installation de googleearth	
			
			xkml="C:\RDC123\Denombrement\Reference\Cartes\CartoIlot.kml";
			if direxist("C:\Program Files (x86)\Google\Google Earth Pro\client") then xrepGoo ="C:\Program Files (x86)\Google\Google Earth Pro\client" ;  endif;
 			if direxist("C:\Program Files (x86)\Google\Google Earth\client") then xrepGoo ="C:\Program Files (x86)\Google\Google Earth\client" ;  endif;           
			if direxist("C:\Program Files\Google\Google Earth Pro\client") then xrepGoo ="C:\Program Files\Google\Google Earth Pro\client" ;  endif;   
			if direxist("C:\Program Files\Google\Google Earth\client") then xrepGoo ="C:\Program Files\Google\Google Earth\client" ;  endif;   
	
       eKML = maketext('"%s\googleearth.exe" ".\%s "',xrepGoo,xkml);
		 execsystem(eKML,maximized,nowait   );ex=1;		
endif ;

end;



PROC CARTOGRAPHIE_FF

preproc 
setfont (valuesets, "Baskerville Old Face", 26,bold);
userbase();
userbar(show);

set attributes (CARTOGRAPHIE) assisted on;
set attributes (CARTOGRAPHIE) assisted off (variable (title));

if demode()=add then addmode=1; endif;
if demode()=modify then addmode=2; endif;


PROC NG02
preproc
clear_labels();
onfocus
rech_com();

setvalueset(@getsymbol(),codes,labels);
setcapturetype($,1);


PROC NG03
preproc
clear_labels();
onfocus
rech_quartier();

setvalueset(@getsymbol(),codes,labels);
setcapturetype($,1);
PROC NG04
preproc
clear_labels();
onfocus
rech_Ilot();

setvalueset(@getsymbol(),codes,labels);
setcapturetype($,1);


PROC NG05
preproc
setfile( FichSaPart,"C:\RDC123\Denombrement\Reference\Ilot.txt") ;  
	     if Filesize( FichSaPart ) > 0 then
		     while FileRead( FichSaPart, strpart ) do
		          if tonumber(strpart[1:2]) = NG01 and tonumber(strpart[3:3]) = NG02  and tonumber(strpart[6:3]) = NG03 and tonumber(strpart[9:5]) = NG04 then  
                  NG05=tonumber(strpart[14:2]);
		          endif ;
		      enddo;
          endif ;
         close( FichSaPart ); 
         noinput;
PROC NG06
preproc
if visualvalue(NG06)>0 then 
	noinput;
else
	numeric xnum v ;
		    xnum=1; 
		    	  
	setfile( FichSaPart,"C:\RDC123\Denombrement\DataCarto\IlotNum.dat") ;  
		     if Filesize( FichSaPart ) > 0 then	 
			     while FileRead( FichSaPart, strpart ) do   
			          if tonumber(strpart[1:5]) = NG04 then  		         
			          xnum=tonumber(strpart[7:3])+1;
			          endif ;		       
			      enddo;
	          endif ;
	       
	v=9;
	NG06=xnum;
endif;

postproc
if v=9 then 
	filewrite(FichSaPart, "%05d %03d",NG04,NG06);
	close( FichSaPart ); 
endif;
v=0;
PROC R10H
preproc
if demode() = add then
  if special(visualvalue($)) then
    x = systime();
    if R10H = notappl then R10H = int(x / 10000);endif;
    if R10M = notappl then R10M = int(x / 100) % 100;endif;
  endif;
endif;
PROC R09J
preproc

if demode() = add then
 	if special(visualvalue ($)) then
 	x = sysdate ();
	  if R09J = notappl then R09J = sysdate( "DD" ); endif;
	  if R09M = notappl then R09M = sysdate( "MM" );endif;
	  if R09A = notappl then R09A = sysdate( "YYYY" );endif;
	endif;
endif;
PROC R13
postproc
rech_com();
rech_quartier();

string image = concat("C:\RDC123\Denombrement\Reference\Ilot\Ilot_",edit("99999",NG04),".jpg",'"' ) ;
execsystem('"' + 'mspaint.exe" "' + image + '"', normal, nowait);
PROC R02A
preproc
clear_labels();
onfocus
rech_Avenue();

setvalueset(@getsymbol(),codes,labels);
setcapturetype($,1);
postproc
R02=labels(R02A);
PROC R02
preproc
numeric v;
v=0;
if strip(R02)="A saisir" then
	R02="";
	v=1;
endif;

postproc
if length(strip(R02))<3 then 
	errmsg("Le nom de la %s semble être incorrect",strip(getlabel(R01_VS1,R01)));
	reenter;
endif;
if strip(R02)="A saisir" then
	errmsg("Saisissez le nom de la %s",strip(getlabel(R01_VS1,R01)));
	reenter;
endif;

//Enregistrer l'avenue dans la base
if v=0 and strip(labels(R02A))<>strip(R02) then
	i=1;
	while i=1  do
		errmsg("Vous avez tenté de modifier le nom de %s, veuillez le préciser ! ",strip(getlabel(R01_VS1,R01)));
		R02=labels(R02A);
		editnote();
		if length(strip(editnote()))>= 3 then
		i=0;
		endif;
		
	enddo;	
endif;
if v=1 then
	setfile( FichSaPart,"C:\RDC123\Denombrement\Reference\Avenue.txt",append) ;  
	filewrite(FichSaPart, "%05d%s",NG04,strip(R02));
	close( FichSaPart ); 
endif;
v=2;
PROC R05

Postproc
if R05 = 2 then
	t=errmsg("Etes- vous sûres que cette parcelle est inhabitée ?")
	select("Oui",continue,"Non",R05);
	if t=1 then
	errmsg("Donnez une observation sur la parcelle inhabitée !");
		editnote();
			if length(strip(editnote()))<6 then
			errmsg("Raison donnée est non valable");
			reenter;
			endif;
	endif;
endif;
PROC R06
if R06>0 then
R06=R06*-1;
endif;
PROC R04A
preproc
R04A=R04;
PROC BATIS000

postproc
if R05 = 2 then 
	if (R04A*2=sum(B03)) then  
	skip to R12FIN ;
	else 
	errmsg("Parcelle non habitée avec bâti habité");
	endif;
endif;

if R05 = 1 and (R04A*2=sum(B03)) then 
	errmsg("Parcelle habitée avec bâti non habité");
	reenter B03(1);
endif;


do varying i=1 until i> R04A
do varying j=i+1 until j> R04A
	if B05(i)=B05(j) and B06(i)=B06(j) then
		errmsg("Deux bâtis ne peuvent pas avoir les mêmes coordonnées géographiques");
	reenter B05(i);
	endif;
enddo;
enddo;
PROC B02

if B02 in 6,8:10 then
	B03 = 2;
	advance to B04;
endif;

if R05 = 1 and B02 in 7,11 then
	//t = errmsg(,B01,strip(getlabel(B02_VS1,B02)))
	t = accept("Etes sûr que ce bâti est habité ?", "Oui,habité", "Non, pas habité");
	if t = 1 then
		B03 = 1;
		advance to B04;
	elseif t = 2 or R04 = 2 then
		B03 = 2;
		advance to B04;
	endif;
	
endif;


if R05 = 1 and B02 = 12 then
	//t = errmsg(,B01,strip(getlabel(B02_VS1,B02)))
	t = accept("Etes sûr que ce bâti n'est pas habité ?", "Oui,habité", "Non, pas habité");
	if t = 1 then
		editnote();
			if length(strip(editnote()))<6 then
			errmsg("Donner plus de description du batiment");
			reenter;
			endif; 
			B03 = 1;
			advance to B04;
	elseif t = 2 or R05 = 2 then
		B03 = 2;
		advance to B04;
	endif;
	
endif;
PROC B03

preproc 

if R05 = 2 then
	B03 = 2;
	noinput;
endif;




PROC B04

Preproc

if B03 = 2 then
	skip to B05;
endif;
PROC B05
if B05>0 then
B05=B05*-1;
endif;
PROC B07
preproc
numeric xB07;
xB07 = gps(distance,R06,R07,B05,B06)  ;/// 1000;
errmsg("Distance entre le point de la parcelle et le bâti est: %d m",xB07);
B07=xB07;
if xB07 in 50:100 then 
	errmsg("Verifier les coordonnées géographiques")
	select("Revoir les coordonnées", B05,"Continuer",continue );
endif;
if xB07 > 100 then
errmsg("La distance entre le point de la parcelle et le bâti est trop grande, veuillez verifier les coordonnées géographiques du bâti");
reenter B05;
endif;
noinput;
PROC R08
preproc
R08=sum(B04);

postproc
if R08=0 then
 skip to R12FIN 
endif;
PROC MENAGES000
postproc

do varying i=1 until i> R08
do varying j=i+1 until j> R08
	if M11(i)=M11(j) and M12(i)=M12(j) then
		errmsg("Deux ménages ne peuvent pas avoir les mêmes coordonnées géographiques");
	reenter M11(i);
	endif;
enddo;
enddo;
PROC M01A

preproc
M01A="Bâti n°";
noinput;
PROC M01

onfocus
numeric z a;
z=1 ;

do varying i=1 until i> R04A
	if B04(i)>0 then
	    do varying j=1 until j> B04(i)
		    if B03(i)=1 then 
		    	  M01(z) = B01(i) ;
				  M02(z) = j;
				  z=z+1 ;
			endif;
	    enddo ;
	 else
	 	a=1;
	 endif;
enddo;
PROC M08
postproc
numeric y;

y = M07+M08;

if y <> M06 then 
	errmsg("Le nombre %d inscrit ne correpond pas au nombre total d'hommes et de femmes dans le ménage de %s,veuillez entrer la bonne valeur", M06,strip(M04))
	select("verifier le nombre total d'hommes", M07,"verifier le nombre total de femmes", M08, "modifier le nombre total d'hommes et de femmes", M06); 
endif;

PROC M09

postproc

if M09 > M07 then
	errmsg("Le nombre de garçons de moins de 5 ans(%d) ne peut être supérieur au nombre total d'hommes dans le ménage(%d)",M09,M07)
	select("Modifier le nombre de garçons de moins de 5 ans",M09," Modifier le nombre total d'hommes",M07)
endif;

if M05 = 1 and M09 = M07 then
	errmsg("Le nombre de garçons de moins de 5 ans(%d) ne peut être égale au nombre total d'hommes dans le ménage(%d)",M09,M07);
	reenter;
endif;
PROC M10

postproc

if M10 > M08 then
	errmsg("Le nombre de filles de moins de 5 ans(%d) ne peut être supérieur au nombre total de femmes dans le ménage(%d)",M10,M08)
	select("Modifier le nombre de filles de moins de 5 ans",M10," Modifier le nombre total de femmes",M08)
endif;


if M05 = 2 and M10 = M08 then
	errmsg("Le nombre de filles de moins de 5 ans(%d) ne peut être égale au nombre total de femmes dans le ménage(%d) étant donné que le chef de ménage est une femme ",M10,M08);
	reenter;
endif;
PROC M11
if M11>0 then
M11=M11*-1;
endif;
PROC M13
preproc
numeric xM13;
xM13 = gps(distance,B05(M01),B06(M01),M11,M12) ;/// 1000;
errmsg("Distance entre le bâti et le ménage est: %d m",xM13);
M13=xM13;
if xM13 in 50:100 then 
	errmsg("Verifier les coordonnées géographiques")
	select("Revoir les coordonnées", M11,"Continuer",continue );
endif;
if xM13 > 100 then
errmsg("La distance entre le point de la parcelle et le bâti est trop grande, veuillez verifier les coordonnées géographiques du bâti");
reenter M11;
endif;
noinput;
PROC R11H
preproc
if demode() = add then
  if special(visualvalue($)) then
    x = systime();
 	R11H = int(x / 10000);
  endif;
endif;  
PROC R11M
preproc
if demode() = add then
  if special(visualvalue($)) then
   x = systime();
   R11M = int(x / 100) % 100;
  endif;
endif;
PROC R12FIN
preproc
rempli_coord();

$ = 2;
postproc

if $ = 1 then 
	errmsg(" Vous pouvez remonter ");
	reenter R01 ;
endif;

if R12FIN = 3 then
	Afficher_carte();
	reenter;
endif

{if $=3 then
execsystem("gps:38.84839,-76.931098,CSPro Team at the U.S. Census Bureau");
//execsystem(concat("gps:",edit("999.999999",M11),",",edit("999.999999",M12)));
endif;}


